!-------------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to calculate fractional standard deviation of condensate

module fsd_condensate_alg_mod

  use constants_mod,        only: i_def
  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  use mesh_mod,             only: mesh_type
  use sci_geometric_constants_mod, only: get_delta_at_wtheta, get_dz_at_wtheta

   implicit none

   private

   public fsd_condensate_alg

contains
  !>@brief   Calculate fractional standard deviation (FSD) condensate
  !>@details Uses cloud fraction, grid box size and convective cloudiness
  !>         to determine variability in condensate for use by
  !>         radiation and microphysics scheme.
  !>         See Hill et al (2015, DOI: 10.1002/qj.2506) for full details.
  !>@param[in,out] f_arr             Array of params for FSD
  !>@param[in,out] cloud_fields      Group of cloud fields
  !>@param[in]     convection_fields Group of convection fields

  subroutine fsd_condensate_alg( f_arr, cloud_fields, convection_fields )

    use fsd_condensate_kernel_mod, only: fsd_condensate_kernel_type
    use io_config_mod,             only: subroutine_timers
    use timer_mod,                 only: timer

    implicit none

    type( field_type ),            intent(inout) :: f_arr
    type( field_collection_type ), intent(in)    :: cloud_fields
    type( field_collection_type ), intent(in)    :: convection_fields

    type( field_type ), pointer :: dz_wth    => null()
    type( field_type ), pointer :: delta     => null()

    type( field_type ), pointer :: area_fraction        => null()
    type( field_type ), pointer :: sigma_mc             => null()
    type( field_type ), pointer :: cca                  => null()

    type( mesh_type), pointer :: mesh => null()

    if ( subroutine_timers ) call timer("fsd_condensate_alg")

    ! Unpack fields
    call cloud_fields%get_field('area_fraction', area_fraction)
    call cloud_fields%get_field('sigma_mc', sigma_mc)
    call convection_fields%get_field('cca', cca)

    mesh => area_fraction%get_mesh()

    delta  => get_delta_at_wtheta(mesh%get_id())
    dz_wth => get_dz_at_wtheta(mesh%get_id())

    call invoke ( fsd_condensate_kernel_type ( sigma_mc,      & ! fsd of condensate
                                               f_arr,         & ! fsd parameters
                                               area_fraction, & ! area cloud fraction
                                               cca,           & ! convective cloud amount
                                               dz_wth,        & ! dz at wtheta
                                               delta ) )        ! edge length

    nullify( mesh )

    if ( subroutine_timers ) call timer("fsd_condensate_alg")

  end subroutine fsd_condensate_alg

end module fsd_condensate_alg_mod
