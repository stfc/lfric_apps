!-----------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Contains central routine for transporting fields.
!> @details Contains routine to transport a field pointing to
!!          particular routines based on the specified transport options.

module transport_field_mod

  use constants_mod,                    only: r_def, r_tran, i_def, l_def
  use field_mod,                        only: field_type
  use r_tran_field_mod,                 only: r_tran_field_type
  use copy_field_alg_mod,               only: copy_field
  use log_mod,                          only: log_event,                       &
                                              LOG_LEVEL_ERROR,                 &
                                              LOG_LEVEL_INFO
  use ffsl_control_alg_mod,             only: ffsl_control
  use split_transport_mod,              only: split_transport_control
  use transport_controller_mod,         only: transport_controller_type
  use transport_counter_mod,            only: transport_counter_type
  use transport_metadata_mod,           only: transport_metadata_type
  use transport_enumerated_types_mod,   only: scheme_mol_3d,                   &
                                              scheme_ffsl_3d,                  &
                                              scheme_split,                    &
                                              direction_3d,                    &
                                              equation_form_conservative,      &
                                              equation_form_advective,         &
                                              equation_form_consistent
  use mol_conservative_alg_mod,         only: mol_conservative_alg
  use mol_advective_alg_mod,            only: mol_advective_alg
  use mol_consistent_alg_mod,           only: mol_consistent_alg


  implicit none

  private

  public :: transport_field, transport_field_r_tran


contains

  !> @brief Central routine for transporting fields.
  !> @details Performs a whole time step, solving the transport equation for
  !!          a field.
  !> @param[in,out] field_np1_rdef     Field to return at end of transport step
  !> @param[in]     field_n_rdef       Field at the start of the transport step
  !> @param[in,out] transport_controller
  !!                                   Encapsulating object containing the
  !!                                   transport counter and precomputations
  !> @param[in,out] transport_metadata Contains the configuration options for
  !!                                   transporting the field
  subroutine transport_field(field_np1_rdef, field_n_rdef,                     &
                             transport_controller, transport_metadata)

    implicit none

    ! Arguments
    type(field_type),                intent(inout) :: field_np1_rdef
    type(field_type),                intent(in)    :: field_n_rdef
    type(transport_controller_type), intent(inout) :: transport_controller
    type(transport_metadata_type),   intent(inout) :: transport_metadata

    ! Local conversions to the r_tran precision
    type(r_tran_field_type) :: field_np1
    type(r_tran_field_type) :: field_n

    ! transfer r_def input to r_tran fields
    call field_np1%initialise( field_np1_rdef%get_function_space() )
    call field_n%initialise( field_n_rdef%get_function_space() )
    call copy_field( field_n_rdef, field_n )

    call transport_field_r_tran(                                               &
            field_np1, field_n, transport_controller, transport_metadata       &
    )

    ! Transfer back to r_def output
    call copy_field( field_np1, field_np1_rdef )

  end subroutine transport_field


  subroutine transport_field_r_tran(field_np1, field_n,                        &
                                    transport_controller, transport_metadata)

    implicit none

    ! Arguments
    type(r_tran_field_type),         intent(inout) :: field_np1
    type(r_tran_field_type), target, intent(in)    :: field_n
    type(transport_controller_type), intent(inout) :: transport_controller
    type(transport_metadata_type),   intent(inout) :: transport_metadata

    ! Internal variables
    integer(kind=i_def)                   :: substep_count
    type(r_tran_field_type),      target  :: field_tmp
    type(r_tran_field_type),      pointer :: field_ptr
    type(transport_counter_type), pointer :: transport_counter

    ! Initialise the counter and set metadata in the transport controller
    ! This also updates the transport metadata, depending on outer loop
    call transport_controller%before_transport_field(transport_metadata)

    ! Get the transport counter
    transport_counter => transport_controller%get_transport_counter()
    call transport_counter%set_field_n(field_n)

    ! If we are substepping, we need an intermediate field corresponding to the
    ! field at the start of each substeps
    ! field_ptr points to the field at the start of the substep
    if (transport_counter%get_num_substeps() > 1) then
      call field_n%copy_field_properties(field_tmp)
      call invoke( setval_X(field_tmp, field_n) )
      field_ptr => field_tmp
    else
      field_ptr => field_n
    end if

    ! ------------------------------------------------------------------------ !
    ! SUBSTEPPING LOOP
    ! ------------------------------------------------------------------------ !

    do substep_count = 1, transport_counter%get_num_substeps()

      call transport_counter%set_field_n(field_ptr)

      ! First choose scheme, and for full 3D schemes then choose equation
      select case ( transport_metadata%get_scheme() )

      ! ---------------------------------------------------------------------- !
      ! Full 3D Method of Lines scheme
      ! ---------------------------------------------------------------------- !
      case ( scheme_mol_3d )
        ! Choose form of transport equation
        select case ( transport_metadata%get_equation_form() )
        case ( equation_form_conservative )
           call mol_conservative_alg(field_np1, field_ptr, transport_controller)

        case ( equation_form_advective )
           call mol_advective_alg(field_np1, field_ptr, transport_controller)

        case ( equation_form_consistent )
           call mol_consistent_alg(field_np1, field_ptr, transport_controller)

        case default
          call log_event(                                                      &
                  'Trying to solve unrecognised form of transport equation',   &
                  LOG_LEVEL_ERROR                                              &
          )

        end select

      ! ---------------------------------------------------------------------- !
      ! Full 3D Flux-Form Semi-Lagrangian scheme
      ! ---------------------------------------------------------------------- !
      case ( scheme_ffsl_3d )
        ! Choose form of transport equation
        select case ( transport_metadata%get_equation_form() )
        case ( equation_form_conservative, equation_form_advective )
          call ffsl_control(field_np1, field_ptr, transport_controller)

        case ( equation_form_consistent )
          call log_event('Consistent 3D FFSL not implemented', LOG_LEVEL_ERROR)

        case default
          call log_event(                                                      &
                  'Trying to solve unrecognised form of transport equation',   &
                  LOG_LEVEL_ERROR                                              &
          )

        end select

      ! ---------------------------------------------------------------------- !
      ! Some split horizontal/vertical transport scheme
      ! ---------------------------------------------------------------------- !
      case ( scheme_split )
        call split_transport_control(field_np1, field_ptr, transport_controller)

      case default
        call log_event(                                                        &
                'Trying to transport with unrecognised scheme',                &
                LOG_LEVEL_ERROR                                                &
        )

      end select

      if (substep_count < transport_counter%get_num_substeps()) then
        call invoke( setval_X(field_tmp, field_np1) )

        ! Increment substep counter
        call transport_counter%inc_split_step_counter()
        call transport_counter%inc_substep_counter()
      end if

    end do ! substep count

    ! Reset any metadata after transporting the field, if necessary
    call transport_controller%after_transport_field()

  end subroutine transport_field_r_tran

end module transport_field_mod
